CMAKE_MINIMUM_REQUIRED( VERSION 3.0 )

PROJECT( flexcore )

IF( CMAKE_SOURCE_DIR STREQUAL CMAKE_CURRENT_SOURCE_DIR )
	SET( STANDALONE TRUE )
ELSE()
	SET( STANDALONE FALSE )
ENDIF()

SET(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake)

OPTION( FLEXCORE_ENABLE_COVERAGE_ANALYSIS "activate gcov based coverage anlysis" OFF )
OPTION( FLEXCORE_ENABLE_TESTS "build unit tests" ${STANDALONE} )

IF( FLEXCORE_ENABLE_COVERAGE_ANALYSIS AND NOT CMAKE_BUILD_TYPE STREQUAL "Debug" )
	MESSAGE( WARNING "Build type is not Debug, code coverage information may be wrong" )
ENDIF()

ADD_COMPILE_OPTIONS(  
    "-Wall"
    "-Wextra"
    "-pedantic"
    $<$<BOOL:${FLEXCORE_ENABLE_COVERAGE_ANALYSIS}>:-fprofile-arcs>
    $<$<BOOL:${FLEXCORE_ENABLE_COVERAGE_ANALYSIS}>:-ftest-coverage>
    )

IF( ${CMAKE_CXX_COMPILER_ID} STREQUAL "Clang" )
	ADD_COMPILE_OPTIONS(
	    "-Wno-unused-local-typedef"
	    )
ENDIF()

SET(THREADS_PREFER_PTHREAD_FLAG ON)
FIND_PACKAGE( Boost 1.55 REQUIRED COMPONENTS log)
FIND_PACKAGE( Threads )

ADD_SUBDIRECTORY( flexcore )
ADD_SUBDIRECTORY( docs )
IF( FLEXCORE_ENABLE_TESTS )
	ADD_SUBDIRECTORY( tests )
	ADD_SUBDIRECTORY( integration_tests )
ENDIF()

